# Maintainer: Dimitri Sabadie <dimitri.sabadie@gmail.com>

_hspkgname=smoothie
pkgname=haskell-$_hspkgname
pkgver=0.2.1
pkgrel=1
pkgdesc='Smooth curves via several splines and polynomials'
arch=('any')
url=https://hackage.haskell.org/package/$_hspkgname
license=('custom:BSD3')
depends=('haskell-linear>=1.16' 'haskell-linear<1.19' \
         'haskell-vector>=0.10' 'haskell-vector<0.11')
makedepends=('ghc<7.11')
install=$pkgname.install
source=("http://hackage.haskell.org/package/$_hspkgname-$pkgver/$_hspkgname-$pkgver.tar.gz")
md5sums=('be0a5eadce74c305192811019a00e31b')

build() {
  cd $_hspkgname-$pkgver
  runhaskell Setup configure -O2 \
    --enable-shared \
    --prefix=/usr \
    --docdir=/usr/share/doc/$pkgname
  runhaskell Setup build
  runhaskell Setup haddock --hyperlink-source
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
}

package() {
  cd $srcdir/$_hspkgname-$pkgver

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -D -m744 CHANGELOG.md $pkgdir/usr/share/$pkgname/CHANGELOG.md

  install -D -m744 register.sh $pkgdir/usr/share/$pkgname/register.sh
  install -D -m744 unregister.sh $pkgdir/usr/share/$pkgname/unregister.sh

  runhaskell Setup copy --destdir=$pkgdir
}
